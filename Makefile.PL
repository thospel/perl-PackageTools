#!/usr/bin/perl -w
# $Id: Makefile.PL 5091 2012-05-15 15:09:26Z hospelt $
use 5.008000;
use strict;
use warnings;
use ExtUtils::MakeMaker;
use FindBin;
use Config qw(%Config);

our $VERSION = '1.007';

# Allows to suppress all program installation with -n (library only)
use Getopt::Std;
our ($opt_n, $opt_y);
getopts("ny") || die "Usage: $0 [-n] [-y]\n";

my @programs_to_install;

if (!$opt_n || $opt_y) {
    print <<EOT	## no critic (UselessNoCritic InputOutput::RequireCheckedSyscalls)

This package comes with programs that I can try
to install in $Config{installscript}.

   Note that you can avoid this question by passing
   the '-n' or '-y' option to 'Makefile.PL'.

EOT
        if !$opt_y;

    # Only do this site_perl substitution for non binary extensions!
    my $site_lib = "$ExtUtils::MakeMaker::Config{siteprefix}/lib/site_perl";
    if ($ExtUtils::MakeMaker::Config{installsitelib} ne $site_lib &&
        grep $_ eq $site_lib, @INC) {
        print <<EOT	## no critic (UselessNoCritic InputOutput::RequireCheckedSyscalls)
Your default perl library install directory is $ExtUtils::MakeMaker::Config{installsitelib}.
This works fine but has the drawback that the library files will not be found
anymore if the perl version gets upgraded. Since $site_lib is in
your default library search path I can use that instead

EOT
            if !$opt_y;
        # arch directories needed to get packlist into /usr/local/lib/site_perl
        map $ExtUtils::MakeMaker::Config{$_} = $site_lib, qw(installsitelib sitelibexp sitearchexp installsitearch) if
            $opt_y ||
            prompt("Use $site_lib as default library install directory ?", "y") =~ /^y/i;
    }

    print <<EOT	## no critic (UselessNoCritic InputOutput::RequireCheckedSyscalls)
This package comes with programs that I can try
to install in $ExtUtils::MakeMaker::Config{installsitebin}.

EOT
        if !$opt_y;
    my %programs = (
        "bin/release_pm"	=> "a simple version manager",
        "bin/makeppd.pl"	=> "a simple ppd generator",
        "bin/any_to_blib"	=> "a pm_to_blib replacement",
    );
    for my $program (sort keys %programs) {
        push @programs_to_install, $program if
            $opt_y ||
            prompt("Install $program, $programs{$program} ?", "y") =~ /^y/i;
    }
}

my $option_file = "$FindBin::Bin/t/options.$^O";
my $new = "$option_file.new.$$";
open(my $fh, ">", $new) || die "Could not open '$new': $!";
printf($fh "KEEP=0\nSTRACE=0\n") ||
    die "Error writing to '$new': $!";
eval {
    close($fh) || die "Could not close '$new': $!";
    rename($new, $option_file) ||
        die "Could not rename '$new' to '$option_file': $!";
};
if ($@) {
    $fh = undef;	# close file if open
    unlink($new) || die "Could not unlink '$new': $! after $@";
    die $@;
}

settings();

WriteMakefile
    (NAME		=> 'PackageTools',
     VERSION_FROM	=> 'lib/PackageTools/Package.pm', # finds $VERSION
     eval {
         ## no critic (UselessNoCritic MagicNumbers)
         ExtUtils::MakeMaker->VERSION(6.55_01);
         1;
     } ? (BUILD_REQUIRES	=> {
         # Only for the tests
         "Test::More"	=> "0.01",
     }) : (),
     PREREQ_PM		=> {
     },
     ABSTRACT		=> 'Package management tools',
     AUTHOR		=> 'Ton Hospel <PackageTools@ton.iguana.be>',
     PL_FILES		=> {},
     EXE_FILES		=> \@programs_to_install,
     $^O eq "MSWin32" ? (
         PM_FILTER	=> '$(PERL) -p -e1',
     ) : (),
     clean		=> {
         FILES => '$(DISTNAME).ppd ppm cover_db nytprof nytprof.out* t/options t/options.* md5-versions.old',
     },
     );

# START MY
# autogenerated by release_pm
BEGIN {
    package MY;

    use vars qw(%postamble);

    $postamble{ppm} = <<'EOT';
ppm: $(DISTNAME).ppd
	makeppd.pl "--perl=$(PERLRUN)" --min_version=1.016 "--zip=$(ZIP)" "--tar=$(TAR)" "--compress=$(COMPRESS)" --leave=ppm "--objects=$(OBJECT)" $(DISTNAME).ppd $(VERSION)
EOT

    $postamble{ppm3} = <<'EOT';
ppm3: $(DISTNAME).ppd
	makeppd.pl "--perl=$(PERLRUN)" --min_version=1.016 "--zip=$(ZIP)" "--tar=$(TAR)" "--compress=$(COMPRESS)" --ppm_version=3 --leave=ppm3 "--objects=$(OBJECT)" $(DISTNAME).ppd $(VERSION)
EOT

    $postamble{ppm4} = <<'EOT';
ppm4: $(DISTNAME).ppd
	makeppd.pl "--perl=$(PERLRUN)" --min_version=1.016 "--zip=$(ZIP)" "--tar=$(TAR)" "--compress=$(COMPRESS)" --ppm_version=4 --leave=ppm4 "--objects=$(OBJECT)" $(DISTNAME).ppd $(VERSION)
EOT

    $postamble{ppd} = <<'EOT';
$(DISTNAME).ppd: all ppd
EOT

    $postamble{cover} = <<'EOT';
cover:
	cover -delete
	mkdir cover_db
	-HARNESS_PERL_SWITCHES=-MDevel::Cover make test
	cover
EOT

    $postamble{critic} = <<'EOT';
critic:
	perlcritic .
EOT

    $postamble{ppm_install} = <<'EOT';
ppm_install: ppm
	ppm install ppm/$(DISTNAME).ppd
EOT

    $postamble{ppm_uninstall} = <<'EOT';
ppm_uninstall:
	ppm uninstall $(DISTNAME)
EOT

    # You will need to add your own entry for README.md
    $postamble{commit} = <<'EOT';
commit: all README.md
	release_pm
	git commit -av
EOT

    $postamble{version_check} = <<'EOT';
all ::
	$(NOECHO) $(FULLPERL) version_check "$(NAME)" "$(DESTINSTALLSITELIB)" "$(VERSION)" "$(VERSION_FROM)"
EOT

undef &postamble;	## no critic (UselessNoCritic AmpersandSigils)
}
{
    package MY;
    sub postamble {
	return shift->SUPER::postamble() . join("\n", @postamble{sort {uc $a cmp uc $b || $a cmp $b } keys %postamble});
    }
}
# END MY

sub settings {
    package MY;
    our %postamble;

    $postamble{README} = <<'EOT';
README.md: bin/release_pm
	pod2readme -f github $< $@

dist: README.md
EOT
}
